# This policy requires at least one CloudTrail trail to be enabled

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants

const = {
	"policy_name":             "cloudtrail-enabled",
	"message":                 "At least one CloudTrail trail should be enabled. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/cloudtrail-controls.html#cloudtrail-3 for more details.",
	"resource_aws_cloudtrail": "aws_cloudtrail",
	"enable_logging":          "enable_logging",
	"name":                    "name",
	"s3_bucket_name":          "s3_bucket_name",
}

# Variables

resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_cloudtrail).resources

# Check if at least one CloudTrail trail is properly configured and enabled
enabled_trails = collection.reject(resources, func(res) {
	enable_logging = maps.get(res, "values.enable_logging", true)

	return enable_logging is true
})

violations = []

if length(resources) is 0 or length(enabled_trails) is length(resources) {
	violations = [{
		"address":        "aws_cloudtrail",
		"module_address": "",
		"message":        const.message,
	}]
}

summary = {
	"policy_name": const.policy_name,
	"violations":  violations,
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
