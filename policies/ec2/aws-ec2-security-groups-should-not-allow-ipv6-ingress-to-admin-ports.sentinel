# This policy ensures EC2 security groups do not allow ingress from ::/0 to remote server administration ports

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants

const = {
	"policy_name":                              "ec2-security-group-no-ipv6-ingress-admin-ports",
	"message":                                  "EC2 security groups should not allow ingress from ::/0 to remote server administration ports (22 or 3389). Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/ec2-controls.html#ec2-54 for more details.",
	"resource_aws_vpc_security_group_ingress_rule": "aws_vpc_security_group_ingress_rule",
	"cidr_ipv6":                                "::/0",
	"admin_ports":                              [22, 3389],
}

# Functions

is_admin_port_exposed = func(res) {
	cidr_ipv6 = maps.get(res, "values.cidr_ipv6", "")
	from_port = maps.get(res, "values.from_port", null)
	to_port = maps.get(res, "values.to_port", null)
	
	if cidr_ipv6 != const.cidr_ipv6 {
		return false
	}
	
	if from_port is null or to_port is null {
		return false
	}
	
	for const.admin_ports as port {
		if from_port <= port and to_port >= port {
			return true
		}
	}
	
	return false
}

# Variables

resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_vpc_security_group_ingress_rule).resources
violations = collection.reject(resources, func(res) {
	return !is_admin_port_exposed(res)
})

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}