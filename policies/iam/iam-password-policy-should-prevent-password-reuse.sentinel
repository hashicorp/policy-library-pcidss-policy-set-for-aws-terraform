# This policy ensures IAM password policy prevents password reuse

# Copyright IBM Corp. 2025
# SPDX-License-Identifier: BUSL-1.1

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants

const = {
	"policy_name": "iam-password-policy-prevents-reuse",
	"message":     "IAM password policy must prevent password reuse. The 'password_reuse_prevention' attribute must be set to a value between 1 and 24 for 'aws_iam_account_password_policy' resources. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/iam-controls.html#iam-16 for more details.",
	"resource_aws_iam_account_password_policy": "aws_iam_account_password_policy",
	"password_reuse_prevention":                "password_reuse_prevention",
}

# Variables

resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_iam_account_password_policy).resources
violations = collection.reject(resources, func(res) {
	password_reuse_prevention = maps.get(res, "values.password_reuse_prevention", null)
	return password_reuse_prevention is not null and password_reuse_prevention >= 1 and password_reuse_prevention <= 24
})

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
