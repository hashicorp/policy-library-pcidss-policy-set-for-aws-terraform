# This policy ensures IAM password policy expires passwords within 90 days or less

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants

const = {
	"policy_name": "iam-password-policy-expires-90-days",
	"message":     "IAM password policy must expire passwords within 90 days or less. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/iam-controls.html#iam-17 for more details.",
	"resource_aws_iam_account_password_policy": "aws_iam_account_password_policy",
	"max_password_age":                         "max_password_age",
	"expire_passwords":                         "expire_passwords",
}

# Variables

resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_iam_account_password_policy).resources
violations = collection.reject(resources, func(res) {
	max_password_age = maps.get(res, "values.max_password_age", null)
	expire_passwords = maps.get(res, "values.expire_passwords", false)
	return max_password_age is not null and max_password_age <= 90 and expire_passwords is true
})

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
