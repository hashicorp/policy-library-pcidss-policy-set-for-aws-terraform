# This policy requires `aws_iam_account_password_policy` resources to have attribute 'require_uppercase_characters' set to true.

# Copyright (c) HashiCorp, Inc.
# SPDX-License-Identifier: BUSL-1.1

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants

const = {
	"policy_name":                         "iam-password-policy-should-have-one-uppercase-letter",
	"message":                             "Attribute 'require_uppercase_characters' must be set to true for 'aws_iam_account_password_policy'. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/iam-controls.html#iam-11 for more details.",
	"resource_aws_iam_account_password_policy": "aws_iam_account_password_policy",
	"require_uppercase_characters":       "require_uppercase_characters",
}

# Functions

get_violations = func(iam_password_policy_resources) {
	return collection.reject(iam_password_policy_resources, func(res) {
		return maps.get(res.values, const.require_uppercase_characters, false) is true
	})
}

# Variables

iam_password_policy_resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_iam_account_password_policy).resources
violations = get_violations(iam_password_policy_resources)

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
