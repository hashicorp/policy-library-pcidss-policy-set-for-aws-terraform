# This policy requires IAM password policy to require at least one uppercase letter

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants

const = {
	"policy_name": "iam-password-policy-uppercase-required",
	"message":     "IAM password policy must require at least one uppercase letter. Attribute 'require_uppercase_characters' must be set to true for 'aws_iam_account_password_policy' resources. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/iam-controls.html#iam-11 for more details.",
	"resource_aws_iam_account_password_policy": "aws_iam_account_password_policy",
	"require_uppercase_characters":             "require_uppercase_characters",
}

# Variables

resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_iam_account_password_policy).resources
violations = collection.reject(resources, func(res) {
	return maps.get(res, "values.require_uppercase_characters", false) is true
})

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
